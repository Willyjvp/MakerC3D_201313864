package org.compi2.codigo3d.parser;

import java_cup.runtime.Symbol;

parser code
{:

    public void syntax_error(Symbol s){
        System.err.println(s);
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.err.println(":(");
        System.err.println(s);
    }

:}

action code
{:
      
:}

terminal String ID, ENTERO, DECIMAL;
terminal TokenC3D MAS, MENOS, MULT, DIV, POT, PAR_IZQ, PAR_DER;
terminal TokenC3D IGUAL_IGUAL, MENOR, MAYOR, MENORI, MAYORI, DIFi;
terminal TokenC3D NOT, AND, OR;
nonterminal NodoC3D inicial, expresion, COND, REL; 
nonterminal String OPERADOR;

precedence left OR;
precedence left AND;
precedence right NOT;
precedence left IGUAL_IGUAL, MENOR, MAYOR, MENORI, MAYORI, DIFi;
precedence left MAS, MENOS;
precedence left MULT, DIV; 
precedence right POT; 

start with inicial;

inicial     ::= {: ControlC3D.reiniciar(); :} COND:conds 
                    {: 
                        RESULT = conds;
                        ControlC3D.agregarC3D("Etiqueta Falso:  " + conds.getCad());
                        ControlC3D.agregarC3D("\nEtiqueta Verdadero:  " + conds.getCad2());
                    :};



COND ::=    COND:izq OR {:ControlC3D.agregarC3D(izq.getCad()+":\n");:} COND:der
                    {: 
                        RESULT = new NodoC3D(der.getCad(),izq.getCad2()+","+der.getCad2());
                    :}
            | COND:izq AND {:ControlC3D.agregarC3D(izq.getCad2()+":\n");:} COND:der
                    {: 
                        RESULT = new NodoC3D(izq.getCad()+","+der.getCad(),der.getCad2());
                    :}
            | NOT  COND:izq   
                    {: 
                        RESULT = new NodoC3D(izq.getCad2(),izq.getCad());
                    :}
            | REL:rel {:  
                RESULT = rel; 
                :};

            
REL::=      REL:relI OPERADOR:opR REL:relD {:
                        String etVerdadero =  ControlC3D.generaEti();
                        String etFalso =  ControlC3D.generaEti();
                        ControlC3D.agregarC3D("if "+relI.getCad() +" "+opR+" "+relD.getCad() +" then go to "+etVerdadero+"\n"+
                                              "go to "+etFalso+"\n");
                        RESULT =  new NodoC3D(etFalso, etVerdadero);
                             :}
            | expresion:exp  {: RESULT = exp; :} ;


OPERADOR::=   MAYOR  {: RESULT = ">"; :}
            | MENOR  {: RESULT = "<";  :}
            | MAYORI {: RESULT = ">=";  :}
            | MENORI {: RESULT = "<=";  :}
            | DIFi   {: RESULT = "!=";  :}
            | IGUAL_IGUAL {: RESULT = "==";  :} ;


expresion::=  expresion:izq MAS expresion:der {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " + " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MENOS expresion:der {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " - " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MULT expresion:der {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " * " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq DIV expresion:der {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " / " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq POT expresion:der {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " ^ " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            
            
            |   MENOS expresion:exp {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = - " + exp.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   PAR_IZQ COND:exp PAR_DER   {: RESULT = exp; :}
            |   ID:id                      {: RESULT = new NodoC3D(id); :}
            |   ENTERO:entero              {: RESULT = new NodoC3D(entero); :}
            |   DECIMAL:decimal            {: RESULT = new NodoC3D(decimal); :}
            ;